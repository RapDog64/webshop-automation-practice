plugins {
    id 'java'
    id 'io.qameta.allure' version '2.8.1'
}

group 'org.demowebshop'
version '1.2'


repositories {
    mavenCentral()
}

ext {
    restAssuredVersion = "4.4.0"
    allureVersion = "2.13.9"
    junitVersion = "5.7.2"
    aspecjweaverVersion = "1.9.6"
    selenideVersion = "5.23.0"
    ownerVersion = "1.0.12"
    slf4jVersion = "1.7.30"
    fakerVersion = "1.0.2"
    jacksonVersion = "2.12.4"
}

allure {
    autoconfigure = true
    aspectjweaver = true
    configuration = "testImplementation"
    version = "${allureVersion}"

    useJUnit5 {
        version = "${allureVersion}"
    }
}

dependencies {

    testImplementation(
            "com.codeborne:selenide:${selenideVersion}",
            "io.qameta.allure:allure-selenide:${allureVersion}",
            "io.qameta.allure:allure-java-commons:$allureVersion",
            "org.aeonbits.owner:owner:${ownerVersion}",
            "io.rest-assured:rest-assured:${restAssuredVersion}",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "org.aspectj:aspectjweaver:${aspecjweaverVersion}",
            "org.junit.jupiter:junit-jupiter-api:${junitVersion}" ,
            "org.junit.jupiter:junit-jupiter-params:${junitVersion}",
            "com.github.javafaker:javafaker:${fakerVersion}",
            "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    )

    testCompileOnly "org.projectlombok:lombok:1.18.20"
    testAnnotationProcessor "org.projectlombok:lombok:1.18.20"
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junitVersion", "org.slf4j:slf4j-simple:${slf4jVersion}")
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(Test) {
    systemProperties += System.properties
    useJUnitPlatform()
    System.setProperty("junit.jupiter.extensions.autodetection.enabled", "true")

    if (System.getProperty("threads") != null) {
        systemProperties += [
                'junit.jupiter.execution.parallel.enabled'                 : true,
                'junit.jupiter.execution.parallel.mode.default'            : 'concurrent',
                'junit.jupiter.execution.parallel.mode.classes.default'    : 'concurrent',
                'junit.jupiter.execution.parallel.config.strategy'         : 'fixed',
                'junit.jupiter.execution.parallel.config.fixed.parallelism': System.getProperty("threads").toInteger()
        ]
    }

    testLogging {
        lifecycle {
            exceptionFormat "short"
        }
    }
}

